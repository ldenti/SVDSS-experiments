configfile: "config.yaml"

from os.path import join as pjoin

REF = config["fa"]
VCF = config["vcf"]
TIER1 = config["tier1"]
# TIER2 = config["tier2"]
# TIER3 = config["tier3"]
TIER23 = config["tier23"]

PARBED = config["par"]
HAP1 = config["hap1"]
HAP2 = config["hap2"]

THREADS = config["threads"]

ODIR = config["out"]

aligners = []
for aligner in config["aligners"]:
    aligners.append(aligner)
callers = []
for caller in config["callers"]:
    callers.append(caller)


modes = {"full" : "/dev/null",
         "tier1": TIER1,
        #  "tier2": TIER2,
        #  "tier3": TIER3,
         "tier23": TIER23}

rule run:
    input:
        expand(pjoin(ODIR, "{aligner}", "results-giab.csv"),
               aligner = aligners),
        expand(pjoin(ODIR, "dipcall-vs-giab", "{mode}"),
               mode = modes),
        expand(pjoin(ODIR, "giab-vs-dipcall", "{mode}"),
               mode = modes)

rule format_table:
    input:
        expand(pjoin(ODIR, "{{aligner}}", "truvari-giab", "{caller}", "{mode}"),
            caller = callers,
            mode = modes)
    output:
        csv = pjoin(ODIR, "{aligner}", "results-giab.csv")
    params:
        wdir = pjoin(ODIR, "{aligner}", "truvari-giab")
    shell:
        """
        python3 format_truvari_table.py {params.wdir} > {output.csv}
        """

###############
### TRUVARI ###
###############

rule truvari:
    input:
        fa = REF,
        vcf = pjoin(ODIR, "{aligner}", "{caller}.vcf.gz"),
        truth = VCF,
        bed = lambda wildcards: modes[wildcards.mode]
    output:
        directory(pjoin(ODIR, "{aligner}", "truvari-giab", "{caller}", "{mode}"))
    params:
        bed_flag = lambda wildcards: "" if modes[wildcards.mode] == "/dev/null" else "--includebed " + modes[wildcards.mode]
    shell:
        """
        truvari bench -c {input.vcf} -b {input.truth} -o {output} -f {input.fa} {params.bed_flag} --passonly -r 1000 -p 0.00 --giabreport
        """

###############
### DIPCALL ###
###############

rule dipcall:
    input:
        fa = REF,
        bed = PARBED,
        hap1 = HAP1,
        hap2 = HAP2
    output:
        vcf = pjoin(ODIR, "dipcall.vcf.gz")
    params:
        wdir = pjoin(ODIR, "dipcall"),
        prefix = pjoin(ODIR, "dipcall", "prefix"),
        mak = pjoin(ODIR, "dipcall.mak")
    threads: THREADS
    shell:
        """
        run-dipcall -t {threads} -x {input.bed} {params.prefix} {input.fa} {input.hap1} {input.hap2} > {params.mak}
        sed -i "s/\$\@ -/\$\@/g" {params.mak}
        mkdir -p {params.wdir}
        make -j 1 -f {params.mak}
        mv {params.prefix}.dip.vcf.gz {output.vcf}
        tabix -p vcf {output.vcf}
        mv {params.prefix}.dip.bed {output.bed}
        """

rule clean_dipcall:
    input:
        vcf = pjoin(ODIR, "dipcall.vcf.gz")
    output:
        vcf = pjoin(ODIR, "dipcall.clean.vcf.gz")
    shell:
        """
        python3 clean_dipcall.py {input.vcf} | bgzip -c > {output.vcf}
        tabix -p vcf {output.vcf}
        """

rule truvari_dipvsgiab:
    input:
        fa = REF,
        vcf = pjoin(ODIR, "dipcall.clean.vcf.gz"),
        truth = VCF,
        bed = lambda wildcards: modes[wildcards.mode]
    output:
        directory(pjoin(ODIR, "dipcall-vs-giab", "{mode}"))
    params:
        bed_flag = lambda wildcards: "" if modes[wildcards.mode] == "/dev/null" else "--includebed " + modes[wildcards.mode]
    shell:
        """
        truvari bench -c {input.vcf} -b {input.truth} -o {output} -f {input.fa} {params.bed_flag} --passonly -r 1000 -p 0.00 --giabreport
        """

rule truvari_giabvsdip:
    input:
        fa = REF,
        vcf = VCF,
        truth = pjoin(ODIR, "dipcall.clean.vcf.gz"),
        bed = lambda wildcards: modes[wildcards.mode]
    output:
        directory(pjoin(ODIR, "giab-vs-dipcall", "{mode}"))
    params:
        bed_flag = lambda wildcards: "" if modes[wildcards.mode] == "/dev/null" else "--includebed " + modes[wildcards.mode]
    shell:
        """
        truvari bench -c {input.vcf} -b {input.truth} -o {output} -f {input.fa} {params.bed_flag} --passonly -r 1000 -p 0.00
        """